import aQute.bnd.osgi.Domain

import com.liferay.gradle.plugins.tasks.ExecuteBndTask
import com.liferay.gradle.util.copy.RenameDependencyClosure

task copySystemPackagesExtraLibs(type: Copy)
task copySystemPackagesExtraManifest(type: Copy)
task executeBnd(type: ExecuteBndTask)

String buildProfile = System.properties["build.profile"]

File systemPackagesExtraManifestFile = file("system.packages.extra.mf")

if (!System.getenv("JENKINS_HOME") || !systemPackagesExtraManifestFile.exists()) {
	systemPackagesExtraManifestFile = new File(buildDir, "system.packages.extra.mf")

	task buildSystemPackagesExtraManifest

	buildSystemPackagesExtraManifest {
		dependsOn classes
		dependsOn copySystemPackagesExtraLibs

		doLast {
			Map<String, String> properties = liferayOSGi.bundleDefaultInstructions

			properties["-check"] = "imports"
			properties["-fixupmessages"] = '"Classpath is empty. Private-Package ...";is:=ignore'
			properties["-include"] = "system.packages.extra.bnd"

			Properties bndProperties = GUtil.loadProperties(file("system.packages.extra.bnd"))

			properties["Bundle-SymbolicName"] = bndProperties.getProperty("Bundle-SymbolicName")
			properties["Bundle-Version"] = project.version

			File portalImplJarFile = configurations.compileOnly.find {
				it.name.startsWith "com.liferay.portal.impl-"
			}

			Domain portalImplJarDomain = Domain.domain(portalImplJarFile)

			properties["Provide-Capability"] = portalImplJarDomain.provideCapability.toString()

			executeBnd.properties properties

			executeBnd.baseDir = projectDir
			executeBnd.classpath = files()
			executeBnd.failOnError = true
			executeBnd.outputFile systemPackagesExtraManifestFile
			executeBnd.resourceDirs = files()
			executeBnd.setSourceDirs files(sourceSets.main.allSource.srcDirs.findAll {it.directory})
			executeBnd.writeManifest = true
		}

		finalizedBy executeBnd
	}
}
else {
	logger.lifecycle "Reusing file {}.", systemPackagesExtraManifestFile.absolutePath
}

bundle {
	instruction "system.packages.extra.manifest.file", relativePath(systemPackagesExtraManifestFile)
}

copySystemPackagesExtraLibs {
	from configurations.compileOnly
	into "lib"
	rename new RenameDependencyClosure(project, configurations.compileOnly.name)
}

dependencies {
	compileOnly group: "biz.aQute.bnd", name: "biz.aQute.bndlib", version: "3.1.0"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.impl", version: "default"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "default"
	compileOnly group: "javax.servlet", name: "javax.servlet-api", version: "3.0.1"
	compileOnly group: "org.osgi", name: "org.osgi.core", version: "6.0.0"
	compileOnly group: "org.springframework", name: "spring-beans", version: "4.1.9.RELEASE"
	compileOnly group: "org.springframework", name: "spring-context", version: "4.1.9.RELEASE"
	compileOnly group: "org.springframework", name: "spring-core", version: "4.1.9.RELEASE"
	compileOnly project(":apps:petra:petra-content")
	compileOnly project(":apps:petra:petra-encryptor")
	compileOnly project(":apps:petra:petra-log4j")
	compileOnly project(":apps:petra:petra-mail")
	compileOnly project(":apps:petra:petra-xml")
	compileOnly project(":core:jaxws-osgi-bridge")
	compileOnly project(":core:osgi-felix-util")
	compileOnly project(":core:osgi-service-tracker-collections")
	compileOnly project(":core:petra:petra-concurrent")
	compileOnly project(":core:petra:petra-executor")
	compileOnly project(":core:petra:petra-function")
	compileOnly project(":core:petra:petra-io")
	compileOnly project(":core:petra:petra-lang")
	compileOnly project(":core:petra:petra-memory")
	compileOnly project(":core:petra:petra-nio")
	compileOnly project(":core:petra:petra-process")
	compileOnly project(":core:petra:petra-reflect")
	compileOnly project(":core:petra:petra-string")
	compileOnly project(":core:portal-app-license-api")
	compileOnly project(":core:registry-api")
	compileOnly project(":core:registry-impl")
}

liferay {
	deployDir = file("${liferayHome}/osgi/core")
}