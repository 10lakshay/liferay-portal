import de.undercouch.gradle.tasks.download.Download

apply plugin: "de.undercouch.download"

task copyInceptionModel(type: Copy)
task downloadInceptionModel(type: Download)

sourceCompatibility = "1.8"
targetCompatibility = "1.8"

classes {
	dependsOn copyInceptionModel
}

copyInceptionModel {
	dependsOn downloadInceptionModel
	from {
		zipTree(downloadInceptionModel.dest)
	}

	includeEmptyDirs = false
	into "classes/META-INF/model"
}

dependencies {
	compileOnly group: "biz.aQute.bnd", name: "biz.aQute.bndlib", version: "3.1.0"
	compileOnly group: "com.liferay.portal", name: "com.liferay.portal.kernel", version: "default"
	compileOnly group: "de.undercouch", name: "gradle-download-task", version: "3.2.0"
	compileOnly group: "org.osgi", name: "org.osgi.core", version: "6.0.0"
	compileOnly group: "org.osgi", name: "org.osgi.service.component.annotations", version: "1.3.0"
	compileOnly group: "org.tensorflow", name: "libtensorflow", version: "1.8.0"
	compileOnly group: "org.tensorflow", name: "libtensorflow_jni", version: "1.8.0"
	compileOnly group: "org.tensorflow", name: "tensorflow", version: "1.8.0"
	compileOnly project(":apps:asset:asset-auto-tagger-api")
	compileOnly project(":apps:static:portal-configuration:portal-configuration-metatype-api")
}

downloadInceptionModel {
	dest new File(buildDir, "inception5h.zip")
	onlyIfNewer true
	overwrite false
	src "https://storage.googleapis.com/download.tensorflow.org/models/inception5h.zip"
}