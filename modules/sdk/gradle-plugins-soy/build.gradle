apply plugin: "com.gradle.plugin-publish"
apply plugin: "org.ysb33r.gradletest"

task copyGradleTestDependencies(type: Copy)

copyGradleTestDependencies {
	from configurations.compile
	into jar.destinationDir
}

dependencies {
	compile group: "com.liferay", name: "com.liferay.gradle.util", version: "1.0.31"

	compile(group: "com.liferay", name: "com.liferay.portal.tools.soy.builder", version: "3.0.4") {
		exclude group: "com.google.template", module: "soy"
		exclude group: "org.codehaus.plexus"
		exclude group: "org.sonatype.plexus"
	}

	compileOnly fileTree(builtBy: [rootProject.tasks.extractGradleApi25], dir: new File(rootProject.buildDir, "gradle-2.5"))
}

gradleTest {
	dependsOn copyGradleTestDependencies
	dependsOn jar

	versions "3.5.1", "4.0.2", "4.1", "4.2.1", "4.3.1", "4.4.1", "4.5.1", "4.6", "4.7", "4.8.1", "4.9", "4.10.2"
}

pluginBundle {
	plugins {
		soyTranslationPlugin {
			description = "The Soy Translation Gradle plugin lets you use a custom localization mechanism in the generated .soy.js files"
			displayName = "Liferay Gradle Plugins Soy Translation"
		}

		soyPlugin {
			displayName = "Liferay Gradle Plugins Soy"
		}
	}

	tags = ["javascript", "js", "soy"]
}